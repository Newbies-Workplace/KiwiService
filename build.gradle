buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.jengelman.gradle.plugins:shadow:$shadow_version"
    }
}

apply plugin: 'kotlin'
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'application'

group 'pl.kiwiteam'
version '0.0.1'
mainClassName = "io.ktor.server.netty.EngineMain"

sourceSets {
    integrationTest {
        kotlin.srcDir "$projectDir/src/integrationTest/kotlin"
        resources.srcDir "$projectDir/src/integrationTest/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    useJUnitPlatform()

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}
check.dependsOn integrationTest

shadowJar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

test {
    useJUnitPlatform()
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

repositories {
    mavenLocal()
    jcenter()
    maven { url 'https://kotlin.bintray.com/ktor' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"

    //ktor core
    implementation "io.ktor:ktor-server-core:$ktor_version"
    implementation "io.ktor:ktor-server-netty:$ktor_version"

    //auth
    implementation "io.ktor:ktor-auth:$ktor_version"
    implementation "io.ktor:ktor-auth-jwt:$ktor_version"

    //security
    implementation "at.favre.lib:bcrypt:$bcrypt_version"

    //serialisation
    implementation "io.ktor:ktor-jackson:$ktor_version"
    
    //valiktor
    implementation "org.valiktor:valiktor-core:$valiktor_version"

    //di
    implementation "org.koin:koin-core:$koin_version"
    implementation "org.koin:koin-ktor:$koin_version"

    //database
    implementation "org.jetbrains.exposed:exposed:$exposed_version"
    implementation "mysql:mysql-connector-java:$mysql_connector_version"

    //test
    testImplementation "org.junit.jupiter:junit-jupiter:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"
    testRuntimeOnly "com.h2database:h2:$h2_version"

    testImplementation "io.ktor:ktor-server-tests:$ktor_version"
    testImplementation "io.ktor:ktor-server-test-host:$ktor_version"
    testImplementation "io.mockk:mockk:$mockk_version"
}
